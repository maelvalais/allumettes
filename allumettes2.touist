;; Requiert touist >= 3.2.0
$NA = 10
;; $NJ = 2 (inutile car ce modèle n'accepte que deux joueurs)
$A = [0..$NA] ;; Ensemble des allumettes
$T = [0..$NA] ;; Ensemble des tours

;; reste(t,n) = il reste n allumettes au temps t
;; prend_2(t) = au temps t, 2 allumettes si vrai, 1 sinon

;; Les 'exists reste(i,$A)' au tour i sont nécessaires pour être certain que
;; quantor fixera ces valeurs après qu'un 'prend_2(i)' est fixé. Sinon,
;; on ne peut jamais savoir quand 'reste(i,0)' est vrai.

exists prend_2(0): \\ ;; joueur 0 (nous)
forall prend_2(1): ;; joueur 1 (adversaire)
exists prend_2(2): \\ ;; joueur 0 (nous)
forall prend_2(3): ;; joueur 1 (adversaire)
exists prend_2(4): \\ ;; joueur 0 (nous)

;; But
not 0_perd \\
;; Conditions initiales
and reste(0,$NA)
and tour_de_0(0) \\
and

;; Resultat de l'action de la prise : si on a pris, il faut baisser
;; le nombre d'allumettes.
bigand $t,$n in $T,$A when $n>=2:
  ((reste($t,$n) and prend_2($t)) => reste($t+1,$n-2))
  and
  ((reste($t,$n) and not prend_2($t)) => reste($t+1,$n-1))
end \\
and

;; On ne peut pas prendre 2 allumettes si il n'en reste plus qu'une.
bigand $t in $T:
  reste($t,1) => (not prend_2($t) and reste($t+1,0))
end \\
and

;; Au moins un nombre d'allumettes par tour.
bigand $t in $T:
  bigor $n in $A:
    reste($t,$n)
  end
end \\
and

;; Au plus un nombre d'allumettes par tour.
bigand $t,$n1 in $T,$A:
  reste($t,$n1) => bigand $n2 in $A when $n1!=$n2: not reste($t,$n2) end
end \\
and

;; Si le joueur j ne peut pas prendre à t et que au coup précédent, joueur j-1
;; a pu prendre une allumette, alors le joueur j a perdu.
;; Le morceau
;;     and not (reste($t-1,1) and reste($t-1,2))
;; est inutile car toujours vrai. Et si on remplaçait ce morceau par
;;     and reste($t-1,1)
;; c'est aussi inutile car reste(_,0) n'apparait qu'une fois.
(0_perd <=>
bigor $t in $T when $t>0:
  tour_de_0($t) and reste($t,0) and not reste($t-1,1)
end) \\
and

;; Si le joueur 0 joue au tour t, alors ça sera à l'autre joueur au tour
;; suivant.
bigand $t in $T:
  not tour_de_0($t) <=> tour_de_0($t+1)
end
